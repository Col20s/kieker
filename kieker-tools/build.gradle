plugins {
	id 'java'
	id 'java-library'
}

// The following lines are necessay to create a test jar
project.configurations {
    testArchives.extendsFrom (testCompile)
}

project.task ('jarTest', type:Jar, dependsOn: project.testClasses, description: 'create a jar from the test source set') {
    from project.sourceSets.test.output
    archiveClassifier = 'test'
}

project.artifacts {
    testArchives project.jarTest
}

// Since the analysis component requires JRE 11, this "tools component" requires JRE 11, too.
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	implementation project(':kieker-common')
	api project(':kieker-analysis')
	api project(':kieker-model')
	api project(':kieker-monitoring')

	implementation 'org.apache.commons:commons-math3:3.6.1'	// used by OPAD
	implementation 'org.apache.commons:commons-lang3:3.12.0'		// used by OPAD

	implementation "com.beust:jcommander:${jcommanderVersion}"

	// TODO this can be removed when all tools have been ported to jcommander
	// - convert-logging-timestamp
	// - resource-monitor
	// https://mvnrepository.com/artifact/commons-cli/commons-cli
	implementation 'commons-cli:commons-cli:1.4'

	implementation 'javax.jms:javax.jms-api:2.0.1'
	implementation 'javax.servlet:javax.servlet-api:4.0.1'

	implementation 'ch.qos.logback:logback-classic:1.1.7'
	implementation "org.slf4j:slf4j-api:$libSlf4jApiVersion"
	implementation 'org.codehaus.groovy:groovy-all:3.0.2'

	// TODO jgraphx is no longer maintained
	// implementation resolveLibrary('jgraphx-*.jar')

	// https://mvnrepository.com/artifact/com.github.yannrichet/Rsession
	implementation 'com.github.yannrichet:Rsession:1.8.3'

	// https://mvnrepository.com/artifact/org.rosuda.REngine/Rserve
	implementation 'org.rosuda.REngine:Rserve:1.8.1'

	// https://mvnrepository.com/artifact/org.rosuda.REngine/REngine
	implementation 'org.rosuda.REngine:REngine:2.1.0'

	// https://mvnrepository.com/artifact/org.apache.activemq/activemq-core
	implementation 'org.apache.activemq:activemq-core:5.7.0'


	// Some dependencies for the HTTP connector of the KDB
	implementation fileTree(dir: rootProject.file('kieker-examples/JavaEEServletContainerExample/jetty/lib'), include: 'jetty-*.jar')

	testImplementation "junit:junit:$libJunitVersion"

	testImplementation project(':kieker-common').sourceSets.test.output
	testImplementation project(':kieker-analysis').sourceSets.test.output
}
