apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: "com.github.hauner.jarTest"

configurations {
	resultJar	// all dependencies in this config are included into all jars. This is necessary for the scripts in bin/ to work, e.g., convertLoggingTimestamp.
}

// Since the analysis component requires JRE 1.8, this "tools component" requires JRE 1.8, too.
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	clover 'org.openclover:clover:4.2.0'

	resultJar 'commons-cli:commons-cli:1.4'

	compile project(':kieker-common')
	api project(':kieker-analysis')
	api project(':kieker-monitoring')	

	compile 'org.apache.commons:commons-math3:3.6.1'	// used by OPAD
	compile 'org.apache.commons:commons-lang3:3.6'		// used by OPAD
	api configurations.resultJar
	
	compile 'com.beust:jcommander:1.71'
	
	compile 'ch.qos.logback:logback-classic:1.1.7'
	compile 'org.slf4j:slf4j-api:1.7.30'
	compile 'org.codehaus.groovy:groovy-all:3.0.2'

	compile resolveLibrary('jgraphx-*.jar')				// this artifact is not available on our repositories
	compile resolveLibrary('Rsession-*.jar')
	compile resolveLibrary('Rserve-*.jar')
	compile resolveLibrary('REngine-*.jar')
	compile resolveLibrary('reflections-*.jar')
	compile resolveLibrary('activemq-core-*.jar')
	
	// Some dependencies for the HTTP connector of the KDB
	compile fileTree(dir: rootProject.file('kieker-examples/JavaEEServletContainerExample/jetty/lib'), include: 'jetty-*.jar')

	// this project depends on the tests of kieker-common, e.g., it requires the class AbstractKiekerTest
	testCompile project (path: ':kieker-common', configuration: 'testArchives')
	// this project depends on the tests of kieker-analysis, e.g., it requires the class BookstoreEventRecordFactory
	testCompile project (path: ':kieker-analysis', configuration: 'testArchives')
	// this project depends on the tests of kieker-analysis, e.g., it requires the class NamedListWriter
	testCompile project (path: ':kieker-monitoring', configuration: 'testArchives')
}

clover {
	report {
		html=true
	}
	
}

