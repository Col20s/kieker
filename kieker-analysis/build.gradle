plugins {
	id 'java'
	id 'java-library'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// The following lines are necessay to create a test jar
project.configurations {
	testArchives.extendsFrom (testCompile)
}

project.task ('jarTest', type:Jar, dependsOn: project.testClasses, description: 'create a jar from the test source set') {
	from project.sourceSets.test.output
	archiveClassifier = 'test'
}

project.artifacts {
	testArchives project.jarTest
}

dependencies {
	api project(':kieker-common')
	api project(':kieker-monitoring')
	api project(':kieker-model')

	api "org.apache.cxf:cxf:$libCxfVersion"			// cxf-bundle

	api 'org.eclipse.emf:org.eclipse.emf.common:2.22.0'
	api 'org.eclipse.emf:org.eclipse.emf.ecore.xmi:2.16.0'
	api 'org.eclipse.emf:org.eclipse.emf.ecore:2.24.0'

	api "com.rabbitmq:amqp-client:$libAmqpVersion"
	api 'nz.ac.waikato.cms.weka:weka-stable:3.6.13' // used by behavior analysis

	api 'de.cau.cs.se.teetime:teetime:3.1.0'

	implementation "com.beust:jcommander:${jcommanderVersion}"

	implementation 'javax.jms:javax.jms-api:2.0.1'
	implementation 'javax.servlet:javax.servlet-api:4.0.1'

	implementation "org.tukaani:xz:$libXzVersion"	// decompression algorithms used by the readers
	implementation 'org.apache.commons:commons-compress:1.20'
	implementation 'org.nanohttpd:nanohttpd:2.3.1'
	implementation 'com.carrotsearch:hppc:0.9.0'

	implementation "org.slf4j:slf4j-api:$libSlf4jApiVersion"

	implementation 'javax.xml.bind:jaxb-api:2.3.1'

	implementation 'com.tinkerpop.blueprints:blueprints-core:2.6.0'

	// this project depends on the tests of kieker-common, e.g., it requires the class AbstractKiekerTest
	testImplementation project(':kieker-common').sourceSets.test.output
	testImplementation 'de.cau.cs.se.teetime:teetime:3.1.0:test'
	testRuntimeOnly 'org.apache.derby:derby:10.13.1.1'	// (10.13.1.1 and above require Java 8, 10.15.2.0 requires Java 11+ and should therefore currently not be used)
}
